plugins {
    id 'org.springframework.boot' version '2.4.0'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'project-report' // For analyzing dependencies when necessary
    id "org.asciidoctor.convert" version "1.5.9.2"
}

group = 'dev.kassie.calendar'
version = '0.0.2-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

configurations {
    // spring-boot-start-web uses Tomcat as the default. To use Jetty instead, we must exclude the tomcat dependency
    compile.exclude module: "spring-boot-starter-tomcat"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.4.0'
    implementation 'org.springframework.boot:spring-boot-starter-jetty:2.4.0'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:2.4.0'

    implementation 'com.google.api-client:google-api-client:1.23.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.23.0'
    implementation 'com.google.apis:google-api-services-calendar:v3-rev305-1.23.0'
    implementation 'org.slf4j:slf4j-api:1.7.7'

    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.5.RELEASE'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.5.RELEASE'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

// package the generated documentation in projectâ€™s jar file
bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}